set(GODZILLA_VERSION @PROJECT_VERSION@)
set(GODZILLA_WITH_MPI @GODZILLA_WITH_MPI@)
set(GODZILLA_WITH_TECIOCPP @GODZILLA_WITH_TECIOCPP@)

@PACKAGE_INIT@

list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}")

include("${CMAKE_CURRENT_LIST_DIR}/godzilla-targets.cmake")
include(FindPackageHandleStandardArgs)
include(CMakeFindDependencyMacro)

find_dependency(fmt 11 REQUIRED)
find_dependency(spdlog 1 REQUIRED)
find_dependency(yaml-cpp 0.8 REQUIRED)
if (GODZILLA_WITH_MPI)
    find_package(MPI REQUIRED)
endif()
find_dependency(PETSc 3.23 REQUIRED)
find_dependency(exodusIIcpp 3 REQUIRED)
find_dependency(mpicpp-lite 2.2 REQUIRED)
find_dependency(HDF5 1.12 REQUIRED COMPONENTS C)
if (GODZILLA_WITH_TECIOCPP)
    find_dependency(teciocpp REQUIRED)
endif()

find_library(GODZILLA_LIBRARY NAMES godzilla HINTS ${PACKAGE_PREFIX_DIR}/lib NO_DEFAULT_PATH)
find_path(GODZILLA_INCLUDE_DIR App.h HINTS ${PACKAGE_PREFIX_DIR}/include/godzilla)

find_package_handle_standard_args(
    godzilla
    REQUIRED_VARS GODZILLA_LIBRARY GODZILLA_INCLUDE_DIR
    VERSION_VAR GODZILLA_VERSION
)

mark_as_advanced(
    GODZILLA_INCLUDE_DIR
    GODZILLA_LIBRARY
    GODZILLA_VERSION
)
